<!--!
    The conventional way to get random_bits for a vehicle is getbits(random_bits, bits_offset, bits_count).
    For articulated vehicles this is not useful when we want the same random result for all parts.
    This provides an alternative to random_bits, which offsets to get the bits from the lead part of the articulated vehicle.
        To use:
        * switch_alternative_var_random_bits(bits_offset, bits_count)
-->

switch (FEAT_TRAINS, SELF, switch_alternative_var_random_bits, bits_offset, bits_count,
                                            [STORE_TEMP(-1 * position_in_articulated_veh, 0x10F),
                                             getbits(var[0x61, 8, 0x00FFFFFF, 0x5F], bits_offset, bits_count)]) {
    <!--! return with nothing else will yield the computed value -->
    return;
}

<!--! to aid managing which bits are used for what, access to them is wrapped in named pseudo vars
      these can then be eye-balled for overlaps
      - note that not all vehicles use all the pseudo vars, and/or not all overlaps are problematic
      - this could be book-kept by getting the bits via Train() or Consist() but seems like over-engineering as of August 2022
-->

<!--! colour set and weathered variant are ok to overlap (probably gets best visual result where vehicles use both together) -->
switch (FEAT_TRAINS, SELF, random_bits_colour_set, switch_alternative_var_random_bits(0, 1)) {
    <!--! return with nothing else to yield the computed value  -->
    return;
}
switch (FEAT_TRAINS, SELF, random_bits_weathered_variant, switch_alternative_var_random_bits(0, 1)) {
    <!--! return with nothing else to yield the computed value  -->
    return;
}

switch (FEAT_TRAINS, SELF, random_bits_base_colour, switch_alternative_var_random_bits(1, 1)) {
    <!--! return with nothing else to yield the computed value  -->
    return;
}

<!--! randomised wagons (those with 'RandomisedConsist' in their classnames) have specific requirements for random bits
      these tend not to overlap with 'normal' wagons, engines etc
-->
switch (FEAT_TRAINS, SELF, random_bits_randomised_wagon_check_neighbour_or_self, switch_alternative_var_random_bits(2, 1)) {
    <!--! return with nothing else to yield the computed value  -->
    return;
}
<!--! the picking from bit 3 onwards is just a wild guess to try and make this independent from colour and weathering choices etc
      bits_counts varies, but is limited upstream by get_wagon_randomisation_candidates() (limited to 4 as of August 2022)
-->
switch (FEAT_TRAINS, SELF, random_bits_randomised_wagon_choose_wagon, bits_count, switch_alternative_var_random_bits(3, bits_count)) {
    <!--! return with nothing else to yield the computed value  -->
    return;
}

<!--! special case -->

<!--! NOTE the units of some articulated consists *will* randomly reverse on build
      in that case it's *correct* for each unit to pick from SELF as the choice is on a per-unit basis, not per consist - don't break that behaviour
      choice of bit overlaps with bits used in randomised wagons, but engines aren't wagons, so should be fine
-->
switch (FEAT_TRAINS, SELF, random_bits_reversed_engine, getbits(random_bits, 2, 1)) {
    <!--! return with nothing else to yield the computed value  -->
    return;
}

<!--! caboose uses random_switch for liveries as of August 2022, so this might collide, but probably fine
      note that this does not use articulated offset - articulated caboose not supported
-->
switch (FEAT_TRAINS, SELF, random_bits_reversed_caboose, getbits(random_bits, 0, 1)) {
    <!--! return with nothing else to yield the computed value  -->
    return;
}

